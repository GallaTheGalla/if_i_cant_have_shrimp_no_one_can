OnTranslate
{
	_talk = reference0
	
	//This is what makes %(embedded_elements) work in script input
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	if !("\![no-autopause]" _in_ _talk)
	{
		_talk = REPLACE(_talk,". ",".\w8 ")
		_talk = REPLACE(_talk,", ",",\w4 ")
		_talk = REPLACE(_talk,"? ","?\w8 ")
		_talk = REPLACE(_talk,"! ","!\w8 ")
		_talk = REPLACE(_talk,"; ",";\w8 ")
		_talk = REPLACE(_talk,": ",":\w8 ")
		_talk = REPLACE(_talk,"~ ","~\w8 ")
	}
	
	//If you want to add anything to OnTranslate, like text replacement, I would put it after this point, generally. Just make sure it's before the TOSTR(_talk)
	
	TOSTR(_talk)
}

//This is just here so you can use anchors to link to websites if you want. You can link to websites with normal menu choice tags! If you do, a handy URL preview will show up in the balloon!
OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

//Hotkeys
OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	elseif reference0 == "t"; RemoteTalk
	elseif reference0 == "r"; RemoteTalk
}

RemoteTalk
{
	"\0\s[1]WHAT DO I LOOK LIKE, A REMOTE CONTROL!?"
}

//Happens about 30 seconds after a script ends, by default, to bring them back to normal poses
OnSurfaceRestore
{
	"\1\s[-1]\0%(idle)"
}

idle
{
	"\0"
	--
	if angrylevel == "max"; "\s[4]"
	elseif angrylevel == "very"; "\s[1]"
	elseif angrylevel == "min"; "\s[2]"
	else; "\s[0]"
}

OnWindowStateRestore
{
	OnSurfaceRestore
}

On_homeurl
{
	"https://raw.githubusercontent.com/GallaTheGalla/if_i_cant_have_shrimp_no_one_can/main/"
}


angrylevel
{
	if aitalkinterval < 0; aitalkinterval = 30
	
	if aitalkinterval <= 30; "max"
	elseif aitalkinterval <= 300; "very"
	elseif aitalkinterval <= 600; "mid"
	else; "min"
}

OnMinuteChange
{
	aitalkinterval -= 30
	
	if aitalkinterval < 30; aitalkinterval = 30
}


//—————————————————————————————— Right click menu links ——————————————————————————————

//Takes an array of alternating names and urls, and formats them for use with the right click menu
FormatLinks : all
{
	for _i = 0; _i < ARRAYSIZE(_argv); _i++
	{
		_argv[_i] //Add the link/title
		//Alternate between adding %ASC(1) or %ASC(2)
		if _i % 2 == 1; "%ASC(2)"
		else; "%ASC(1)"
	}
}

On_sakura.recommendsites
{
	FormatLinks(recommendsites_sakura)
}

//These are the websites linked in the top option of the sakura's right click menu. The format is simple; write the label of the link on the left followed by a semicolon, and then on the right, the url to link to. These should be separate strings.
recommendsites_sakura : array
{
	"Link label";	"https://link.example.com/link/to/whatever/here/"
}

On_sakura.portalsites 
{
	FormatLinks(portalsites_sakura)
}

//These are the websites linked in the second option of the sakura's right click menu. Same as above
portalsites_sakura : array
{
	"Another link label";	"https://link.example.com/link/to/whatever/here/again/"
	"A third link label";	"https://link.example.com/link/to/whatever/a/third/time/you/can/add/as/many/as/you/want/"
}

On_kero.recommendsites
{
	FormatLinks(recommendsites_kero)
}

//These are the websites linked in the first option of the kero's right click menu. Same as above
recommendsites_kero : array
{
	"These labels can be anything you want";	"https://link.example.com/link/to/whatever/here/once/more/"
}