//—————————————————————————————— Network Update ——————————————————————————————
//Starting an update
OnUpdateBegin
{
	"\0\b[0]%(idle)Checking for updates.\w8"
}

//reference0 is the number of new files, starting from 0
OnUpdateReady
{
	reference0++ //Number of files starts from 0, so this displays the correct amount
	
	//plural checks, adds an s and changes is to are if there's more than 1 file
	_s = ""; _are = "is"; if reference0 != 1; _s = "s"; _are = "are"
	
	"\0\b[0]%(idle)There %(_are) %(reference0) new file%(_s).\w8"
}

//When the update finishes. Don't forget to initialize any new variables you've created! I highly highly recommend using OnInitialize for this (it's in boot.dic), in case the user updates via the ghost explorer or some other means.
//reference0 is 'none' if there were no new files to update with, reference1 is a list of new file names, separated by commas
OnUpdateComplete
{
	if reference0 == "none"
	{
		"\0\b[0]%(idle)There are no new files."
	}
	else
	{
		"\0\b[0]%(idle)Update complete."
	}
}

//reference0 is the reason the update failed
OnUpdateFailure //If the update fails
{
	if reference0 == "timeout" //If it timed out
	{
		"\0\b[0]%(idle)Connection timed out."
	}
	elseif reference0 == "md5 miss" //If the update files have not been uploaded correctly and don't match the updates2.dau
	{
		_file = SPLITPATH(reference1)[2] + SPLITPATH(reference1)[3] //This gets the name and extension of the offending file
		
		"\0\b[0]%(idle)The MD5 values don't match.\w8 Contact the ghost author and let them know that '%(_file)' is giving an MD5 error."
	}
	elseif reference0 == "artificial" //If the user cancels the update by double clicking the balloon
	{
		"\0\b[0]%(idle)Update cancelled."
	}
	else //Any other errors
	{
		"\0\b[0]%(idle)Error while updating:\w4 ""%(reference0)""."
	}
}