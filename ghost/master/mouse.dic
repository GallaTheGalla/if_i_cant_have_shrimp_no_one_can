//Separate from the menus because you could put other interactions here, like poking or booping or punching or asking about various hotspots
OnMouseDoubleClick
{
	if reference5 == 0 //reference5 is the mouse button being clicked, in this case a left click
	{
		//reference3 is which character is being clicked
		//If you want to add double click interactions, reference4 will be the name of the hitbox the mouse is over
		if reference3 == 0
		{
			if reference4 == "head"; OnRandomizeColors
			else; OnSakuraMenu
		}
	}
}

OnRandomizeColors
{
	_currentcolor = CurrentColor
	_newcolor = _currentcolor
	if ARRAYSIZE(AvailableColors) > 1
	{
		while _newcolor == _currentcolor
		{
			_newcolor = ANY(AvailableColors)
		}
	}
	
	if DefaultBalloon
	{
		_balloons = (0,1,2)
		_currentballoon = BalloonColor
		_newballoon = _currentballoon
		while _newballoon == _currentballoon
		{
			_newballoon = ANY(_balloons)
		}
		BalloonColor = _newballoon
	}
	
	"\0\b[0]\s[0]\![bind,Color,%(_newcolor),1]SQUACK"
}

OnMouseMove
{
	if reference4 == "head" && reference3 == 0
	{
		stroke++
		
		if stroke % 40 == 0
		{
			if reference3 == 0; OnPet
		}
	}
}

OnPet
{
	if stroke % petrequire == 0; aitalkinterval += 30
	
	_e = emoji
	"\0\b[0]\s[2]%(_e) %(flamingonoises) %(_e)"
}

petrequire
{
	40 * 3
}

OnMouseLeaveAll
{
	if stroke > 40 && stroke < petrequire
	{
		"\0\b[0]\s[1]I DIDN'T SAY YOU COULD STOP!"
	}
	stroke = 0
}